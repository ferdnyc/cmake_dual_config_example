@PACKAGE_INIT@

cmake_minimum_required(VERSION 3.15)
include(CMakeFindDependencyMacro)

set(xmltest_known_comps static shared)
set(xmltest_comp_static NO)
set(xmltest_comp_shared NO)
foreach (xmltest_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (xmltest_comp IN_LIST xmltest_known_comps)
        set(xmltest_comp_${xmltest_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "xmltest does not recognize component `${xmltest_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (xmltest_comp_static AND xmltest_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "xmltest `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(xmltest_static_targets "${CMAKE_CURRENT_LIST_DIR}/xmltest-static-targets.cmake")
set(xmltest_shared_targets "${CMAKE_CURRENT_LIST_DIR}/xmltest-shared-targets.cmake")

macro(xmltest_load_targets type)
    find_dependency(tinyxml2 REQUIRED COMPONENTS ${type})
    if (NOT EXISTS "${xmltest_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "xmltest `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${xmltest_${type}_targets}")
endmacro()

if (xmltest_comp_static)
    xmltest_load_targets(static)
elseif (xmltest_comp_shared)
    xmltest_load_targets(shared)
elseif (DEFINED xmltest_SHARED_LIBS AND xmltest_SHARED_LIBS)
    xmltest_load_targets(shared)
elseif (DEFINED xmltest_SHARED_LIBS AND NOT xmltest_SHARED_LIBS)
    xmltest_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${xmltest_shared_targets}")
        xmltest_load_targets(shared)
    else ()
        xmltest_load_targets(static)
    endif ()
else ()
    if (EXISTS "${xmltest_static_targets}")
        xmltest_load_targets(static)
    else ()
        xmltest_load_targets(shared)
    endif ()
endif ()
